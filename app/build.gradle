apply plugin: 'com.android.application'

def retrieveProp(String key) {
    try {
        Properties props = new Properties()
        props.load(new FileInputStream("private.properties"))
        def val = props.getProperty(key)
        return val
    } catch (FileNotFoundException ignored) {
        return ''
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "seniordesign.lostdogcollar"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        resValue "string", "GOOGLE_API_KEY", "\"${retrieveProp('GOOGLE_API_KEY')}\""
        resValue "string", "FACEBOOK_APP_ID", "\"${retrieveProp('FACEBOOK_APP_ID')}\""
        buildConfigField "String", "SERVER_IP", "\"${retrieveProp('SERVER_IP')}\""
        buildConfigField "String", "SENDER_ID", "\"${retrieveProp('SENDER_ID')}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'com.facebook.android:facebook-android-sdk:4.7.0'
    compile 'com.android.support:cardview-v7:23.1.1'
}

apply plugin: 'com.google.gms.google-services'
